resource "azurerm_mssql_firewall_rule" "allow_azure_services" {
  name             = "AzureServices"
  server_id        = azurerm_mssql_server.db-server.id
  start_ip_address = "0.0.0.0"
  end_ip_address   = "0.0.0.0"
}

resource "azurerm_mssql_server" "db-server" {
  name                         = "sonarqube-sql-server"
  resource_group_name          = azurerm_resource_group.sonarqube.name
  location                     = local.location
  version                      = "12.0"
  administrator_login          = "4dm1n157r470r"
  administrator_login_password = var.SQL_SERVER_PASSWORD
  tags                         = local.devtesttags
  minimum_tls_version          = "1.2"
}

resource "azurerm_mssql_database" "db" {
  name           = "sonarqubedb"
  server_id      = azurerm_mssql_server.db-server.id
  collation      = "SQL_Latin1_General_CP1_CS_AS"
  license_type   = "LicenseIncluded"
  max_size_gb    = 2
  sku_name       = "S0"
  enclave_type   = "VBS"
  zone_redundant = true
  tags           = local.devtesttags

  short_term_retention_policy {
    retention_days = 14
  }

  long_term_retention_policy {
    weekly_retention = "PT0S"
  }

  lifecycle {
    prevent_destroy = true
  }
}

resource "azurerm_mssql_virtual_network_rule" "sql-rule" {
  name      = "sql-vnet-rule"
  server_id = azurerm_mssql_server.db-server.id
  subnet_id = data.azurerm_subnet.sql-subnet.id
}

resource "azurerm_mssql_database_extended_auditing_policy" "sonarqube" {
  database_id                             = azurerm_mssql_database.db.id
  storage_endpoint                        = azurerm_storage_account.sonarqube.primary_blob_endpoint
  storage_account_access_key              = azurerm_storage_account.sonarqube.primary_access_key
  storage_account_access_key_is_secondary = false
  retention_in_days                       = 90
}

resource "azurerm_mssql_server_security_alert_policy" "sonarqube" {
  resource_group_name        = azurerm_resource_group.sonarqube.name
  server_name                = azurerm_mssql_server.db-server.name
  state                      = "Enabled"
  storage_endpoint           = azurerm_storage_account.sonarqube.primary_blob_endpoint
  storage_account_access_key = azurerm_storage_account.sonarqube.primary_access_key
  email_account_admins       = true
  retention_days             = 20
}

resource "azurerm_mssql_server_vulnerability_assessment" "sonarqube" {
  server_security_alert_policy_id = azurerm_mssql_server_security_alert_policy.sonarqube.id
  storage_container_path          = "${azurerm_storage_account.sonarqube.primary_blob_endpoint}${azurerm_storage_container.db_vul_assess.name}/"
  storage_account_access_key      = azurerm_storage_account.sonarqube.primary_access_key

  recurring_scans {
    enabled                   = true
    email_subscription_admins = true
  }
}